/*

   Generic punching bag script. Only used in Brotherhood bunker right now.

      Using the scenery will give a one-time bonus to unarmed combat.

*/

#include "define.h"

#define NAME                    SCRIPT_ZSPNCHBG

#include "command.h"


procedure start;
procedure use_p_proc;

procedure learn_skill;

#define LVAR_Herebefore         (0)

procedure start begin
end

procedure use_p_proc begin
   variable
      training_anim, sound;

	// Source animation:
	reg_anim_clear(source_obj);
	reg_anim_begin();

		training_anim := random(0, 1);
		if (training_anim == 0) then
			training_anim := ANIM_throw_punch;
		else
			training_anim := ANIM_kick_leg;
		sound := sfx_build_char_name(source_obj, training_anim, snd_contact);

	reg_anim_animate(source_obj, training_anim, -1);
	reg_anim_play_sfx(source_obj, Sound, 0);
	reg_anim_callback(learn_skill);
	reg_anim_end();

	// Punchbag animation:
	reg_anim_clear(self_obj);
	reg_anim_begin();
	reg_anim_animate(self_obj, ANIM_stand, -1);
	reg_anim_animate_reverse(self_obj, ANIM_stand, -1);
	reg_anim_end();
end

procedure learn_skill begin
    if (global_var(GVAR_BONUS_PUNCHING_BAG) == 0) then begin
      inc_global_var(GVAR_BONUS_PUNCHING_BAG);

      gfade_out(600);
         critter_mod_skill(dude_obj, SKILL_UNARMED_COMBAT, 5);
         game_time_advance(ONE_GAME_HOUR);
         display_msg(g_mstr(950));
      gfade_in(600);
   end
end
