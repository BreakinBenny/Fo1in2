/*

   Hub - Sid, the guard in Beth's gunshop

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_SID
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure damage_p_proc;
procedure look_at_p_proc;
procedure timed_event_p_proc;
procedure Sid00;

variable tmp_hostile;
variable Only_Once := 1;
variable whisper_event := 0;

#define EVENT_FIRED     (1)

import variable Sid_Ptr;
import variable Beth_Ptr;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      Sid_Ptr := self_obj;
      set_self_team(TEAM_HUB_FAR_GO_TRADERS);
      set_self_ai(AI_GUARD);
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   get_reaction
   if (whisper_event == 0) then
      call Sid00;
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure damage_p_proc begin
   set_global_var(GVAR_ENEMY_HUB, 1);
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure timed_event_p_proc begin
   if (fixed_param == EVENT_FIRED) then begin
      whisper_event := 0;
      float_msg(Beth_Ptr, message_str(SCRIPT_HBETH, 378), FLOAT_MSG_RED);
      float_msg(self_obj, mstr(111), FLOAT_MSG_YELLOW);
   end
end

procedure Sid00 begin
   variable LVar0 := 0;
   LVar0 := random(1, 7);
   if (LVar0 == 1) then begin
      if ((game_time_hour >= 800) and (game_time_hour < 1200)) then begin
         float_msg(self_obj, mstr(101), FLOAT_MSG_YELLOW);
      end
      else begin
         if ((game_time_hour >= 1200) and (game_time_hour < 1600)) then begin
            if (dude_is_male) then begin
               float_msg(self_obj, mstr(102), FLOAT_MSG_YELLOW);
            end
            else begin
               float_msg(self_obj, mstr(103), FLOAT_MSG_YELLOW);
            end
         end
         else begin
            float_msg(self_obj, mstr(104), FLOAT_MSG_YELLOW);
         end
      end
   end
   else if (LVar0 == 2) then begin
      float_msg(self_obj, mstr(105), FLOAT_MSG_YELLOW);
   end
   else if (LVar0 == 3) then begin
      float_msg(self_obj, mstr(106), FLOAT_MSG_YELLOW);
      float_msg(Beth_Ptr, message_str(SCRIPT_HBETH, 374), FLOAT_MSG_RED);
   end
   else if (LVar0 == 4) then begin
      float_msg(self_obj, mstr(107), FLOAT_MSG_YELLOW);
      float_msg(Beth_Ptr, message_str(SCRIPT_HBETH, 375), FLOAT_MSG_RED);
   end
   else if (LVar0 == 5) then begin
      float_msg(self_obj, mstr(108), FLOAT_MSG_YELLOW);
      float_msg(Beth_Ptr, message_str(SCRIPT_HBETH, 376), FLOAT_MSG_RED);
   end
   else if (LVar0 == 6) then begin
      float_msg(self_obj, mstr(109), FLOAT_MSG_YELLOW);
      float_msg(Beth_Ptr, message_str(SCRIPT_HBETH, 377), FLOAT_MSG_RED);
   end
   else begin
      whisper_event := 1;
      float_msg(self_obj, mstr(110), FLOAT_MSG_YELLOW);
      add_timer_event(self_obj, game_ticks(5), EVENT_FIRED);
   end
end


