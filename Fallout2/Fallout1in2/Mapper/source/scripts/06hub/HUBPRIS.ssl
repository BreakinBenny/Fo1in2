/*

   Hub - Prisoners in the jail cells

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_HUBPRIS
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure damage_p_proc;

#define LVAR_Herebefore          (4)

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_PRISONERS);
      set_self_ai(AI_LOSER);

      if map_first_run and fixt_enabled then begin
         if (self_tile == 23068) then begin
            move_to(self_obj, 22666, self_elevation);
         end
         if (self_tile == 22070) then begin
            move_to(self_obj, 21667, self_elevation);
         end
         if (self_tile == 21666) then begin
            move_to(self_obj, 21865, self_elevation);
         end
      end

   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   get_reaction
   float_msg(self_obj, mstr(random(101, 107)), FLOAT_MSG_RED);
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure damage_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_HUB, 1);
   end
end
