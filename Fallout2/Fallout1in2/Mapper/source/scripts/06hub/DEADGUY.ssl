/*

   Hub - "Deadguy" - cut-content

      NOTE:

         Appears in Decker's hideout now once the underground has been eliminated.

*/

/* Include Files */
#include "define.h"
#include "area/hub.h"
#include "maps/hubdwntn.h"

#define NAME                    SCRIPT_DEADGUY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure combat;

procedure Deadguy00;
procedure Deadguy01;
procedure Deadguy02;
procedure Deadguy03;
procedure Deadguy04;
procedure Deadguy05;
procedure Deadguy06;
procedure Deadguy07;
procedure Deadguy08;
procedure DeadguyEnd;

#define LVAR_Herebefore       (4)
#define LVAR_StealMoney       (5)
#define LVAR_Disappear        (6)

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   if not(fo1in2_cut_characters_enabled) then destroy_object(self_obj);
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_HUB_SKAG);
      set_self_ai(AI_HUB_PEASANTS);
      critter_attempt_placement(self_obj, self_tile, 2);
   end

   if (global_var(GVAR_UNDERGROUND_STATUS) == 1) then begin
      if not(is_loading_game) then begin
         set_self_visible;
      end
   end
end

procedure map_enter_p_proc begin
   if not(is_loading_game) then begin
      if (local_var(LVAR_Disappear) == 1) then begin
         if (local_var(LVAR_StealMoney) == 0) then begin
            inc_general_rep(5);
            inc_hub_rep(REP_BONUS_SPARED_DEADGUY);
         end
         destroy_object(self_obj);
      end
      else if ((map_var(MVAR_REWARD_FOR_DECKER) > 1) and (local_var(LVAR_Disappear) == 0)) then begin
         critter_attempt_placement(self_obj, self_tile, 1);
      end
      else begin
         critter_attempt_placement(self_obj, self_tile, 2);
      end
   end
end

procedure map_update_p_proc begin
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   get_reaction
   if (local_var(LVAR_Herebefore) == 1) then begin
      call Deadguy02;
   end
   else begin
      set_local_var(LVAR_Herebefore, 1);
      start_dialog_at_node(Deadguy00);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure combat begin
   tmp_hostile := 1;
end

procedure Deadguy00 begin
   Reply(101);
   BOption(102, Deadguy01, 4);
   if (local_var(LVAR_StealMoney) == 0) then begin
      NOption(103, Deadguy03, 4);
   end
   GOption(104, Deadguy07, 4);
   NLowOption(105, Deadguy06);
end

procedure Deadguy01 begin
   NMessage(106);
end

procedure Deadguy02 begin
   float_msg(self_obj, mstr(107), FLOAT_MSG_RED);
end

procedure Deadguy03 begin
   set_local_var(LVAR_StealMoney, 1);
   set_local_var(LVAR_Disappear, 1);
   dec_general_rep(2);

   dude_caps_adjust(20);
   Reply(108);
   NOption(109, DeadguyEnd, 4);
   BOption(110, Deadguy04, 4);
end

procedure Deadguy04 begin
   Reply(111);
   BOption(112, Deadguy05, 4);
   NOption(113, DeadguyEnd, 4);
end

procedure Deadguy05 begin
   if (dude_is_male) then begin
      NMessage(114);
   end
   else begin
      NMessage(115);
   end
end

procedure Deadguy06 begin
   if (dude_is_male) then begin
      NMessage(118);
   end
   else begin
      NMessage(119);
   end
end

procedure Deadguy07 begin
   set_local_var(LVAR_Disappear, 1);
   if (dude_is_male) then begin
      Reply(122);
   end
   else begin
      Reply(123);
   end
   GOption(126, Deadguy08, 4);
   BOption(127, combat, 4);
end

procedure Deadguy08 begin
   if (dude_is_male) then begin
      NMessage(128);
   end
   else begin
      NMessage(129);
   end
end

procedure DeadguyEnd begin
end
