/*

   Hub - Mike, Old Town guard - cut-content

*/

/* Include Files */
#include "define.h"
//#include "area/hub.h"
#include "sfall/sfall.h"

#define NAME                    SCRIPT_MIKE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure damage_p_proc;
procedure combat_p_proc;
procedure look_at_p_proc;

procedure Mike01;
procedure Mike02;
procedure Mike03;
procedure Mike04;
procedure Mike05;
procedure Mike06;

#define LVAR_Home_Tile                  (5)
#define LVAR_Home_Rotation              (6)

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_HUB_POLICE);
      set_self_ai(AI_GUARD);
   end
   art_change_fid_num(self_obj, FID_HMGLCM);
end

procedure map_enter_p_proc begin
   if map_first_run then begin
      set_local_var(LVAR_Home_Tile, self_tile);
      set_local_var(LVAR_Home_Rotation, self_cur_rot);
   end

   if (days_since_visited > 2) then begin
      critter_attempt_placement(self_obj, local_var(LVAR_Home_Tile), self_elevation);
      self_rotate(local_var(LVAR_Home_Rotation));
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_HUBOLDTN);

   dude_look_at_self;
   get_reaction
   start_dialog_at_node(Mike01);
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure damage_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_HUB, 1);
      tmp_hostile := 1;
   end
end

procedure combat_p_proc begin
   if REP_ENEMY_HUB then begin
      tmp_hostile := 1;
      attack(dude_obj);
   end
   set_global_var(GVAR_PC_WANTED, 1);
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure Mike01 begin
   set_scr_name(mstr(200));
   Reply(101);
   NLowOption(102, Mike02);
   NOption(103, Mike03, 4);
   if looking_for_waterchip then begin
      NOption(104, Mike04, 4);
   end
   GOption(105, Mike05, 4);
end

procedure Mike02 begin
   NMessage(106);
end

procedure Mike03 begin
   Reply(107);
   if looking_for_waterchip then begin
      NOption(108, Mike04, 4);
   end
   NOption(109, Mike05, 4);
end

procedure Mike04 begin
   Reply(110);
   BOption(111, Mike06, 4);
   NOption(112, Mike03, 4);
   GOption(113, Mike05, 4);
end

procedure Mike05 begin
end

procedure Mike06 begin
   call combat;
end
