/*

   Hub - Old Town, Kidnappers related to bos quest.

*/

/* Include Files */
#include "define.h"
//#include "area/hub.h"
#include "maps/huboldtn.h"

#define NAME                    SCRIPT_HUBCAPTR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

#define LVAR_Talked                     (4)
#define LVAR_Home_Tile                  (5)
#define LVAR_Home_Rotation              (6)

// Is the player inside the house?
// coords: top left, top right, bottom left, bottom right
#define dude_inside     tile_in_tile_rect(18740, 18728, 21140, 21128, dude_tile)

variable tmp_hostile;
variable Only_Once := 1;
variable GunLine;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_HUB_UNDERGROUND);
      set_self_ai(AI_HUB_UNDERGROUND);
   end
end

procedure map_enter_p_proc begin
   if map_first_run then begin
      set_local_var(LVAR_Home_Tile, self_tile);
      set_local_var(LVAR_Home_Rotation, self_cur_rot);
   end
   else begin
      critter_attempt_placement(self_obj, local_var(LVAR_Home_Tile), self_elevation);
      self_rotate(local_var(LVAR_Home_Rotation));
   end
   set_map_var(MVAR_Captor_Float, 0);
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end

   if (dude_inside and self_can_see_dude) then begin
      tmp_hostile := 1;
      if (map_var(MVAR_Captor_Float) == 0) then begin
         set_map_var(MVAR_Captor_Float, 1);
         GunLine := random(105, 108);
         if (GunLine == 108) then begin
            if (dude_is_female) then GunLine := 109;
         end
         float_msg(self_obj, mstr(GunLine), FLOAT_MSG_YELLOW);
      end
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   set_local_var(LVAR_Talked, local_var(LVAR_Talked) + 1);
   if (local_var(LVAR_Talked) < 4) then begin
      float_msg(self_obj, mstr(100 + local_var(LVAR_Talked)), FLOAT_MSG_YELLOW);
   end
   else begin
      float_msg(self_obj, mstr(104), FLOAT_MSG_YELLOW);
      call combat;
   end
end

procedure destroy_p_proc begin
   set_map_var(MVAR_Total_Captors, map_var(MVAR_Total_Captors) + 1);
   inc_evil_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end
