/*

   Hub - Bartender in the Maltese Falcon

*/

/* Include Files */
#include "define.h"

#define NAME                    SCRIPT_FALCON1

#include "command.h"
#include "modreact.h"

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure Falcon00;
procedure Falcon01;
procedure Falcon02;
procedure Falcon03;
procedure Falcon04;
procedure Falcon05;
procedure Falcon05a;
procedure Falcon06;
procedure Falcon06a;
procedure Falcon07;
procedure Falcon07a;
procedure Falcon09;
procedure Falcon11;
procedure Falcon14;
procedure Falcon15;
procedure Falcon16;
procedure FalconEnd;

variable tmp_hostile;
variable Only_Once := 1;
variable First_Water;
variable item;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_HUB_UNDERGROUND);
      set_self_ai(AI_MERCHANT);
   end
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   get_reaction
   start_dialog_at_node(Falcon00);
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure Falcon00 begin
   Reply(101);
   NOption(102, Falcon05, 4);
   NOption(103, Falcon06, 4);
   NOption(104, Falcon07, 4);
   NOption(106, Falcon04, 4);
   NLowOption(107, Falcon03);
end

procedure Falcon01 begin
   NMessage(108);
end

procedure Falcon02 begin
   NMessage(109);
end

procedure Falcon03 begin
   if (dude_is_male) then begin
      NMessage(110);
   end
   else begin
      NMessage(111);
   end
end

procedure Falcon04 begin
   NMessage(112);
end

procedure Falcon05 begin
   Reply(113);
   NOption(114, Falcon05a, 4);
   NOption(115, Falcon11, 4);
end

procedure Falcon05a begin
   if (dude_caps >= 2) then begin
      call Falcon09;
   end
   else begin
      call Falcon01;
   end
end

procedure Falcon06 begin
   Reply(116);
   NOption(117, Falcon06a, 4);
   NOption(115, Falcon11, 4);
end

procedure Falcon06a begin
   if (dude_caps < 5) then begin
      call Falcon01;
   end
   else begin
      call Falcon14;
   end
end

procedure Falcon07 begin
   Reply(118);
   NOption(117, Falcon07a, 4);
   NOption(115, Falcon11, 4);
end

procedure Falcon07a begin
   if (dude_caps < 10) then begin
      call Falcon01;
   end
   else begin
      call Falcon16;
   end
end

procedure Falcon09 begin
   if (First_Water) then begin
      Reply(mstr(139) + mstr(140) + mstr(141));
   end
   else begin
      Reply(mstr(136) + mstr(137) + mstr(138));
   end
   dude_caps_adjust(-2);
   First_Water := 1;
   call Falcon15;
end

procedure Falcon11 begin
   if (random(0, 1)) then begin
      NMessage(mstr(151) + mstr(152));
   end
   else begin
      NMessage(mstr(153) + mstr(154));
   end
end

procedure Falcon14 begin
   dude_caps_adjust(-5);
   item := create_object(PID_BEER, 0, 0);
   add_obj_to_inven(dude_obj, item);
   Reply(mstr(160) + mstr(161));
   call Falcon15;
end

procedure Falcon15 begin
   NOption(102, Falcon05, 4);
   NOption(103, Falcon06, 4);
   NOption(104, Falcon07, 4);
   NOption(106, Falcon04, 4);
   NLowOption(107, Falcon03);
end

procedure Falcon16 begin
   dude_caps_adjust(-10);
   item := create_object(PID_BOOZE, 0, 0);
   add_obj_to_inven(dude_obj, item);
   Reply(mstr(163) + mstr(164));
   call Falcon15;
end

procedure FalconEnd begin
end
