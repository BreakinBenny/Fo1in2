/*

   Mariposa -

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_KRUPPER
#define TOWN_REP_VAR            (GVAR_TOWN_REP_MBASE)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure destroy_p_proc;

procedure krupper00;
procedure krupper01;
procedure krupper02;
procedure krupper03;
procedure krupper04;
procedure kruppercombat;

#define LVAR_Herebefore          (4)

variable tmp_hostile;
variable initial := 0;

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_SUPER_MUTANT_AGGRESSIVE);
   end
end

procedure critter_p_proc begin
   if ((local_var(LVAR_Herebefore) == 0) and self_can_see_dude) then begin
      dialogue_system_enter;
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data(tma_source_generic, TMA_MSG_MBASE, -1, -1, 997, 999);

   dude_look_at_self;

   coc_disguise_check;
   if DISGUISED then
      call krupper01;
   else
      call kruppercombat;
   set_local_var(LVAR_Herebefore, 1);
end

procedure timed_event_p_proc begin
   if (self_can_see_dude) then begin
      tmp_hostile := 1;
   end
end

procedure destroy_p_proc begin
   inc_inv_evil_crit
end

procedure krupper00 begin
   float_msg(self_obj, mstr(101), FLOAT_MSG_NORMAL);
   call kruppercombat;
end

procedure krupper01 begin
   start_gdialog(434, self_obj, 4, -1, -1);
   gsay_start;
      Reply(102);
      NOption(103, krupper03, 4);
      NOption(104, kruppercombat, 4);
      NLowOption(105, krupper02);
   gsay_end;
   end_dialogue;
end

procedure krupper02 begin
   NMessage(106);
   call kruppercombat;
end

procedure krupper03 begin
   Reply(107);
   NOption(108, krupper02, 4);
   NOption(109, krupper04, 4);
end

procedure krupper04 begin
   NMessage(110);
   add_timer_event(self_obj, game_ticks(20), 1);
end

procedure kruppercombat begin
   tmp_hostile := 1;
end
