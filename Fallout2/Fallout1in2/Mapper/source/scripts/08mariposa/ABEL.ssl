/*

   Mariposa - Abel, super mutant in the barracks. Has the force field switch item.

*/

/* Include Files */
#include "define.h"
#include "area/mbase.h"
#include "maps/mbstrg12.h"

#define NAME                    SCRIPT_ABEL
#define TOWN_REP_VAR            (GVAR_TOWN_REP_MBASE)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;

procedure Abel00;
procedure Abel01;
procedure Abel02;
procedure Abel03;
procedure Abel04;
procedure Abel05;
procedure Abel06;
procedure Abel07;
procedure Abel08;
procedure AbelCombat;

#define LVAR_Herebefore       (4)
#define LVAR_SpawnToggle      (5)

import variable field_change

variable tmp_hostile;
variable initial := 0;
variable toggle;

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_SUPER_MUTANT_AGGRESSIVE);
      self_rotate(3);
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   if (self_can_see_dude or (((dude_tile / 200) > 134) and ((dude_tile % 200) > 108) and (tile_distance_objs(self_obj, dude_obj) <= 10)) and (local_var(LVAR_Herebefore) == 0)) then begin
      dialogue_system_enter;
   end

   // Cut-content. Item isn't used in vanilla Fo1.
   if ((self_item_count(PID_TOGGLE_SWITCH) == 0) and (local_var(LVAR_SpawnToggle) == 0)) then begin
      set_local_var(LVAR_SpawnToggle, 1);
      toggle := create_object(PID_TOGGLE_SWITCH, 0, 0);
      add_obj_to_inven(self_obj, toggle);
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data(NAME, 1000, 970, 970, 980, 981);

   dude_look_at_self;
   start_gdialog(359, self_obj, 4, -1, -1);
   gsay_start;
   if dude_wearing_coc_robe then begin
      call Abel06;
   end
   else begin
      if (local_var(LVAR_Herebefore) == 1) then begin
         call Abel08;
      end
      else begin
         call Abel00;
      end
   end
   set_local_var(LVAR_Herebefore, 1);
   gsay_end;
   end_dialogue;
end

procedure destroy_p_proc begin
   inc_inv_evil_crit
end

procedure Abel00 begin
   Reply(102);
   NLowOption(103, Abel01);
   NOption(104, Abel03, 4);
   NOption(105, Abel04, 5);
end

procedure Abel01 begin
   Reply(106);
   NLowOption(107, Abel02);
end

procedure Abel02 begin
   Reply(108);
   NLowOption(109, Abel03);
end

procedure Abel03 begin
   NMessage(110);
   call AbelCombat;
end

procedure Abel04 begin
   if ((map_var(MVAR_Field2b_Status) == 1) and ((dude_tile / 200) > 134) and ((dude_tile % 200) > 108)) then begin
      Reply(111);
      NOption(112, Abel05, 5);
   end
   else begin
      NMessage(111);
      call AbelCombat;
   end
end

procedure Abel05 begin
   NMessage(113);
   call AbelCombat;
end

procedure Abel06 begin
   if ((map_var(MVAR_Field2b_Status) == 1) and ((dude_tile / 200) > 134) and ((dude_tile % 200) > 108)) then begin
      Reply(114);
      NLowOption(115, Abel01);
      NOption(116, Abel07, 4);
   end
   else begin
      NMessage(114);
   end
end

procedure Abel07 begin
   NMessage(117);
end

procedure Abel08 begin
   NMessage(118);
   call AbelCombat;
end

procedure AbelCombat begin
   if ((map_var(MVAR_Field2b_Status) == 1) and ((dude_tile / 200) > 134) and ((dude_tile % 200) > 108)) then begin
      set_force_fields_off;
      set_map_var(MVAR_Field2b_Status, 0);
   end
   tmp_hostile := 1;
end
