//
// ---TRAP SCRIPT---  Sduibek
//
// I think this is unused.
//
#include "define.h"
#include "command.h"

procedure start;
procedure spatial_p_proc;//    script_action == spatial_proc
procedure use_skill_on_p_proc;

import variable removal_ptr;

variable triggered;
variable test;


procedure start begin
end

procedure spatial_p_proc
begin
   if source_is_dude then begin
      if (dude_tile != self_tile) then begin
         triggered := 0;
      end
      else begin
         if (triggered == 0) then begin
            triggered := 1;
            if ((dude_is_walking) and dude_stat_success(STAT_pe, 0) and dude_stat_success(STAT_ag, 0)) then begin
               display_msg(message_str(SCRIPT_VMONOWIR, 100));
               set_map_var(13, 1);
            end
            else begin
               display_msg(message_str(SCRIPT_VMONOWIR, 101));
               if (dude_is_walking) then begin
                  critter_dmg(dude_obj, random(10, 20), DMG_laser);
               end
               else begin
                  critter_dmg(dude_obj, random(15, 30), DMG_laser);
               end
               if (not(dude_stat_success(STAT_lu, 0))) then begin
                  if (random(0, 1)) then begin
                     display_msg(message_str(SCRIPT_VMONOWIR, 102));
                     critter_injure(dude_obj, DAM_CRIP_LEG_RIGHT);//    DAM_CRIP_LEG_RIGHT     8
                  end
                  else begin
                     display_msg(message_str(SCRIPT_VMONOWIR, 103));
                     critter_injure(dude_obj, DAM_CRIP_LEG_LEFT);//    DAM_CRIP_LEG_LEFT      4
                  end
               end
            end
         end
      end
   end
end

procedure use_skill_on_p_proc
begin
   if ((map_var(7) == 1) and (map_var(13) == 0)) then begin
      script_overrides;
      test := roll_vs_skill(dude_obj, SKILL_TRAPS, -20)
      if (is_success(test)) then begin
         display_msg(message_str(SCRIPT_VMONOWIR, 104));
         set_map_var(13, 1);
         move_to(self_obj, 7000, 0);
         removal_ptr := self_obj;
      end
      else begin
         if (is_critical(test)) then begin
            display_msg(message_str(SCRIPT_VMONOWIR, 106));
            critter_dmg(dude_obj, 1, DMG_laser);
         end
         else begin
            display_msg(message_str(SCRIPT_VMONOWIR, 105));
         end
      end
   end
end
