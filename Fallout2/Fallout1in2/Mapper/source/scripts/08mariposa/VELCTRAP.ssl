/*

   Military Base - Vats, electrical trap spatial script

   NOTE:

      Template script!
      Script shared with VEXDTRAP.ssl and VPLSTRAP.ssl

*/

#define NAME                     SCRIPT_TRAPFLOR

#ifndef SCRIPTNAME
   #define SCRIPTNAME            SCRIPT_VELCTRAP
   #define trap_damage           critter_dmg(source_obj, random(15, 40), DMG_electrical)
#endif

/* Include Files */
#include "define.h"
#include "command.h"
#include "area/mbase.h"

/* Standard Script Procedures */
procedure start;
procedure spatial_p_proc;

#define LVAR_TrapStatus          (0)

variable Passed_Times;

procedure start begin
end

procedure spatial_p_proc begin
   if source_in_party then begin
      if mbase_full_alert then begin
         if (skill_success(source_obj, SKILL_TRAPS, 0) and (local_var(LVAR_TrapStatus) == 0)) then begin
            give_xp(EXP_TRAPS_NORMAL);
            reg_anim_clear(source_obj);
            if (source_obj != dude_obj) then begin
               display_msg(proto_data(obj_pid(source_obj), 1) + mstr(100));
            end
            else begin
               Passed_Times := Passed_Times + 1;
               if (Passed_Times > 2) then begin
                  set_local_var(LVAR_TrapStatus, 1);
               end
               display_msg(mstr(101));
            end
         end
         else begin
            if (local_var(LVAR_TrapStatus) == 0) then begin
               set_local_var(LVAR_TrapStatus, 1);
               trap_damage;
            end
         end
      end
   end
end
