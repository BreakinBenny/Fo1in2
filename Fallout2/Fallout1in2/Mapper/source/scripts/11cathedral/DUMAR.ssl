/*

   Cathedral - Dumar, cut content

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_DUMAR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure dumar00;
procedure dumar00a;
procedure dumar01;
procedure dumar02;
procedure dumar03;
procedure dumar04;
procedure dumar05;
procedure dumar06;
procedure dumar07;
procedure dumarend;

#define LVAR_Herebefore       (4)

variable Only_Once;
variable tmp_hostile;

procedure start begin
   if Only_Once then begin
      set_self_team(TEAM_CATHEDRAL);
      set_self_ai(AI_CHILDREN_TRUE);
      Only_Once := 0;
   end
   if not(fo1in2_cut_characters_enabled) then destroy_object(self_obj);
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure destroy_p_proc begin
   inc_neutral_critter
end

procedure damage_p_proc begin
   display_msg(mstr(116));
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_COC);
   dude_look_at_self;

   if (tmp_hostile == 1) then begin
      attack(dude_obj);
   end
   else begin
      if (local_var(LVAR_Herebefore)) then begin
         //start_dialog_at_node(dumar05);
         floater(113);
      end
      else begin
         set_local_var(LVAR_Herebefore, 1);
         start_dialog_at_node(dumar00);
      end
   end
end

procedure dumar00 begin
   variable hurt := 0;
   hurt := dude_max_hp - dude_cur_hp;

   Reply(101);
   call dumar00a;
end

procedure dumar00a begin
   NLowOption(102, dumar04);
   NOption(103, dumar03, 6);
   BOption(104, dumar06, 4);
   NOption(105, dumar07, 4);
   BOption(106, dumar04, 4);
   NOption(107, dumar05, 5);
   NOption(108, dumar02, 6);
end

procedure dumar01 begin
   NMessage(109);
   call dumarend;
end

procedure dumar02 begin
   NMessage(110);
   call dumarend;
end

procedure dumar03 begin
   NMessage(111);
   call dumarend;
end

procedure dumar04 begin
   NMessage(112);
   call dumarend;
end

procedure dumar05 begin
   //NMessage(113);
   //call dumarend;
   Reply(113);
   call dumar00a;
end

procedure dumar06 begin
   NMessage(114);
   tmp_hostile := 1;
   call dumarend;
end

procedure dumar07 begin
   NMessage(115);
   call dumarend;
end

procedure dumarend begin
end
