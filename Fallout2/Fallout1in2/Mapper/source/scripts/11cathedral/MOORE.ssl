/*

   Cathedral - Moore, psychic prisoner

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_GIDEON
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure dialog_end;
procedure moore00;
procedure moore01;
procedure moore02;
procedure moore03;
procedure moore04;

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_PSYKERS);
      set_self_ai(AI_BRAINWASHED_CHILDREN);
   end

   // Change to black guy with grey shirt:
   //art_change_fid_num(self_obj, FID_NMBGRY);
end

procedure combat begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc begin
   set_tma_data(tma_source_generic, TMA_MSG_MBASE, -1, -1, 997, 999);

   dude_look_at_self;
   get_reaction

   if (local_var(4) == 0) then begin
      start_dialog_at_node(moore00);
   end
   else begin
      start_dialog_at_node(moore01);
   end
   set_local_var(4, 1);
end

procedure destroy_p_proc begin
   inc_inv_evil_crit
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(200));
end

procedure dialog_end begin
end

procedure moore00 begin
   Reply(202);
   ROption(203, moore03, 8);
   ROption(204, moore02, 4);
   if (has_rep_berserker) then begin
      ROption(205, moore04, 4);
   end
   ROption(206, dialog_end, 4);
   RLowOption(207, moore04);
end

procedure moore01 begin
   Reply(208);
   ROption(209, moore03, 8);
   ROption(210, moore04, 4);
   ROption(211, moore02, 4);
   if (has_rep_berserker) then begin
      ROption(212, moore02, 4);
   end
   ROption(213, dialog_end, 4);
end

procedure moore02 begin
   Reply(214);
   ROption(216, moore03, 4);
   ROption(217, moore04, 4);
end

procedure moore03 begin
   gsay_message(NAME, 218, RND_REACTION);
   call dialog_end;
end

procedure moore04 begin
   gsay_message(NAME, 219, RND_REACTION);
   call dialog_end;
end
