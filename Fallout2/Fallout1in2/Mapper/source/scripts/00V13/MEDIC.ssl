/*

   Vault 13 - the doctor

*/

#include "define.h"
#include "area/vault13.h"

#define NAME                    SCRIPT_MEDIC
#define TOWN_REP_VAR            (GVAR_TOWN_REP_V13)

#include "command.h"
#include "modreact.h"

procedure start;
procedure map_enter_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure MedicBadPlayer; // Medic00
procedure MedicIDontCare; // Medic01
procedure MedicLowIntelligenceBadPlayer; // Medic02
procedure MedicSeriouslyWounded; // Medic03
procedure MedicMainMenu; // Medic04
procedure MedicCheckPlayerInjuries; // Medic04a
procedure MedicStupidGiveStimpaks; // Medic05
procedure Medic06;
procedure MedicPoisonedHealRequest; // Medic07
procedure Medic08;
procedure Medic08a;
procedure Medic09;
procedure Medic10;
procedure Medic11;
procedure MedicRequestHealing; // Medic12
procedure MedicYouSeemOk; // Medic13
procedure Medic14;
procedure Medic15;
procedure MedicStartHealing; // Medic16
procedure MedicHealPlayer; // Medic17
procedure Medic18;
procedure Medic19;
procedure Medic20;
procedure Medic21;
procedure Medic22;
procedure Medic23;
procedure Medic24;
procedure MedicRadiationFadeOutIn; // Medic25
procedure MedicRadiationHealRequest; // Medic26
procedure Medic27;
procedure Medic28;
procedure Medic29;

procedure GiveStimpaks;
procedure GiveDrugs;
procedure MedicEnd;

procedure Node2000;
procedure Node3000;

variable intensity := 0;
variable removal := 0;
variable Minutes := 0;
variable Hours := 0;
variable TempMinutes := 0;
variable rads := 0;    // Not sure if we really need to save this in a variable?
variable PoisAmt := 0; // Not sure if we really need to save this in a variable?
variable rounded := 0;
variable IfRounded := 0;
variable home_tile := 13084;// was 13881

variable Only_Once := 0;

#define LVAR_RefuseToHelp        (4)  // The doctor refuses to help the player
#define LVAR_HostileBehavior     (5)  // Has attacked or stolen
#define LVAR_HasTalkedBefore     (6)  // Set after talked for the first time
#define LVAR_ReceivedSupplies    (7)  // 0 = No, 1 = Once, 2 = Twice or more
#define LVAR_DayReceivedSupplies (8)  // GAME_TIME_IN_DAYS when the player received drugs or stimpaks last time
#define LVAR_Had_Toe_Removed    (11)  // Mutated toe war removed from the player

#define HasTalkedBefore (local_var(LVAR_HasTalkedBefore))
#define RefusesToHelp   (local_var(LVAR_RefuseToHelp)) // The doctor refuses to help the player

#define player_damage (dude_max_hp - dude_cur_hp)
#define is_poisoned   (PoisAmt > 0)
#define is_irradiated (rads > 0)

procedure start begin
   if (Only_Once == 0) then begin
      Only_Once := 1;
      set_self_team(TEAM_VAULT13);
   end
end

procedure map_enter_p_proc begin
   if (v13_invaded) then begin
      invasion_kill_critter_V13;
   end
end

procedure critter_p_proc begin
   if self_tile != home_tile then begin
      self_walk_to_tile(home_tile);
   end
   else begin
      // Why is it checking for 2?
      if local_var(10) != 2 then begin
         if self_tile == home_tile then begin
            self_rotate(2);
            set_local_var(10, 2);
         end
      end
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      set_local_var(LVAR_RefuseToHelp, 1);
      set_local_var(LVAR_HostileBehavior, 1);
   end
end

procedure destroy_p_proc begin
   set_global_var(GVAR_VAULT_13_HOSTILE, 1);
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(136));
end

procedure pickup_p_proc begin
   float_msg(self_obj, mstr(160), FLOAT_MSG_RED);
   set_local_var(LVAR_HostileBehavior, 1);
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_VAULT13);

   debug("Current damage: " + player_damage);
   debug("Current poison: " + dude_poison_stat + ". " + "Current rads: " + dude_rads + ". " + "Current time: " + game_time_hour + ". ");

   dude_look_at_self;
   if not(RefusesToHelp) then begin
      get_reaction
      start_gdialog(184, self_obj, 4, -1, -1);
      gsay_start;
      if (local_var(5) == 1) or REP_ENEMY_VAULT13 then begin
         call MedicBadPlayer;// bad player
      end
      else begin
         PoisAmt := dude_poison_stat;
         rads := dude_rads;
         if (player_damage > 7) then begin
            call MedicSeriouslyWounded;// go right to healing
         end
         else begin
            call MedicMainMenu;// MAIN MENU
         end
      end
      gsay_end;
      end_dialogue;
   end
   else begin // RefusesToHelp, LVAR_RefuseToHelp = 1
      display_msg(mstr(159)); // The medic refuses to speak to you
   end
   if not(local_var(LVAR_HasTalkedBefore)) then begin
      set_local_var(LVAR_HasTalkedBefore, 1);
   end
   if debug_mode then begin
      display_msg("Current poison: " + dude_poison_stat + ". " + "Current rads: " + dude_rads + ". " + "Current time: " + game_time_hour + ". ");
   end
end

procedure MedicBadPlayer begin
   Reply(100); // I can't believe what you've done
   BOption(101, MedicIDontCare, 4); // I don't care. I'm hurt, heal me
   BLowOption(102, MedicLowIntelligenceBadPlayer);
end

procedure MedicIDontCare begin
   set_local_var(LVAR_RefuseToHelp, 1);
   BigDownReact
   BMessage(103); // I will not help you
end

procedure MedicLowIntelligenceBadPlayer begin
   set_local_var(LVAR_RefuseToHelp, 1);
   BigDownReact
   BMessage(104); // Go away
end

procedure MedicSeriouslyWounded begin
   Reply(105); // Oh my, you're seriously wounded
   NOption(164, MedicStartHealing, 1);
end

procedure MedicMainMenu begin
   if waterchip_returned then begin
      Reply(157);// thanks for saving us
   end
   else begin
      if not(HasTalkedBefore) then begin // The first time we talk to the doctor
         Reply(113);// What can i do for you?
         NOption(114, MedicRequestHealing, 4); // I need healin
      end
      else begin// hello again!
         Reply(mstr(106) + dude_name + mstr(107));
         NOption(108, Medic06, 4); // It's going ok
         NOption(109, MedicRequestHealing, 4); // I need help
      end

      if(global_var(GVAR_HAVE_MUTATED) == 1) then
         NOption(2000, Node2000, 004);
   end
   if is_poisoned then begin// i've been poisoned
      NOption(161, MedicPoisonedHealRequest, 5);
   end
   if is_irradiated then begin// i'm irradiated
      NOption(162, MedicRadiationHealRequest, 5);
   end
   if waterchip_returned then begin// you're welcome. bye.
      NOption(163, MedicEnd, 4);
   end
   if dude_is_stupid then begin// STUPID
      NLowOption(117, MedicRequestHealing); // Need healing
      if not(local_var(6)) then begin
         BLowOption(118, Medic15);// info on healing STUPID
      end
      else begin
         BLowOption(171, Medic15);// info on healing
      end
   end
   if (local_var(LVAR_ReceivedSupplies) == 0) then begin
      set_local_var(LVAR_DayReceivedSupplies, GAME_TIME_IN_DAYS);
      NLowOption(110, MedicStupidGiveStimpaks);// i want drugs (STUPID)
      NOption(115, Medic08, 5);// i want drugs
   end
   else if (local_var(LVAR_ReceivedSupplies) == 1) then begin
      set_local_var(LVAR_DayReceivedSupplies, GAME_TIME_IN_DAYS);
      NLowOption(110, Medic28);// i want drugs (STUPID)
      NOption(116, Medic11, 5);// i want more stimpaks
   end
   else if (local_var(LVAR_ReceivedSupplies) == 2) then begin
      // Check if 30 days or more have elapsed since we last asked for stimpaks.
      if ((GAME_TIME_IN_DAYS) - local_var(LVAR_DayReceivedSupplies)) >= 30 then begin
         set_local_var(LVAR_DayReceivedSupplies, GAME_TIME_IN_DAYS);
         NLowOption(110, Medic29);// i want drugs (STUPID)
         NOption(116, Medic29, 5);// i want more stimpaks
      end
      else begin
         BLowOption(110, Medic27);// i want drugs (STUPID)
         NOption(116, Medic27, 5);// i want more stimpaks
      end
   end
end

procedure MedicCheckPlayerInjuries begin
   if (player_damage > 3) or is_poisoned then begin
      call Medic14;
   end
   else begin
      call MedicYouSeemOk;
   end
end

procedure MedicStupidGiveStimpaks// STUPID CHARACTER, GIVE SIMPAKS
begin
   if local_var(LVAR_ReceivedSupplies) == 0 then begin
      set_local_var(LVAR_ReceivedSupplies, 1);
   end
   NMessage(111);
   call GiveStimpaks;
end

procedure Medic06//{112}{}{Good, keep up the good work. And let me know if you need anything.}
begin
   NMessage(112);
end

procedure MedicPoisonedHealRequest// poisoned heal request
begin
   poison(dude_obj, -PoisAmt);
   gfade_out(1);
   game_time_advance(10 * 60 * PoisAmt * 3);
   gfade_in(1);
   NMessage(156);
   if (player_damage) <= 3 then begin
      critter_heal(dude_obj, dude_max_hp);
   end
end

procedure Medic08//{119}{}{I'm not authorized to give you any. Sorry.}
begin
   Reply(119);
   BOption(120, MedicEnd, 4); // Thanks for nothing
   NOption(121, Medic08a, 6);
end

procedure Medic08a// skillcheck for drug demand
begin
   if local_var(7) == 0 then begin
      set_local_var(7, 1);
   end
   if dude_skill_success(SKILL_SPEECH, -10) then begin
      call Medic10;
   end
   else begin
      call Medic09;
   end
end

procedure Medic09//{122}{}{Sorry, I can't give them to you.}
begin
   set_local_var(7, 3);
   BMessage(122);
end

procedure Medic10//{123}{}{Well, since you put it that way. But don't tell anyone that I let you have these. They are extremely addictive and dangerous to use.
begin
   Reply(123);
   NOption(124, GiveDrugs, 4);
end

procedure GiveStimpaks begin
   variable Stimpak := 0;
   Stimpak := create_object(PID_STIMPAK, 0, 0);
   add_mult_objs_to_inven(dude_obj, Stimpak, 4 - difficulty_level);
end

procedure Medic11 begin
   if local_var(7) == 1 then begin
      set_local_var(7, 2);
   end
   if dude_stat_success(STAT_ch, 0) or dude_skill_success(SKILL_SPEECH, -20) then begin
      call Medic29;
   end
   else begin
      NMessage(125);//{125}{}{Stimpaks are being rationed. You are welcome to what we have in the medical container on the wall. Take as many as you need, but use them sparingly. It will be a while before we'll be able to get you some more.}
   end
end

procedure MedicRequestHealing // "hmm let's see..."
begin
   Reply(126);
   NOption(164, MedicCheckPlayerInjuries, 1);
end

procedure MedicYouSeemOk// don't need healing if small dmg
begin
   NMessage(127);
end

procedure Medic14//"{128}{}{You look hurt. Here, let me help you.}"
begin
   Reply(128);
   NOption(164, MedicStartHealing, 1);
end

procedure Medic15//{129}{}{If you need healing, come back here to me or use some of the stimpaks we have. You can get more stimpaks from the wall.}
begin
   NMessage(129);
end

procedure MedicStartHealing// "lie down and we'll start"
begin
   Reply(130);
   NOption(164, MedicHealPlayer, 1);
end

procedure MedicHealPlayer// healing
begin
   if is_poisoned then begin
      poison(dude_obj, -PoisAmt);
   end
   gfade_out(1);
   if is_poisoned then begin
      game_time_advance(10 * 60 * PoisAmt * 3);
   end
   critter_heal(dude_obj, player_damage);
   critter_uninjure(dude_obj, (DAM_CRIP_LEG_LEFT bwor DAM_CRIP_LEG_RIGHT bwor DAM_CRIP_ARM_LEFT bwor DAM_CRIP_ARM_RIGHT));
   game_time_advance(10 * 60 * player_damage * 5);
   gfade_in(1);
   NMessage(131);
   if (player_damage) <= 3 then begin
      critter_heal(dude_obj, dude_max_hp);
   end
   if rads then begin
      call Medic18;
   end
end

procedure Medic18 begin
   game_time_advance(10 * 60);
   if (rads > 400) then begin
      intensity := mstr(132);
   end
   else if (rads > 200) then begin
      intensity := mstr(133);
   end
   else if (rads > 100) then begin
      intensity := mstr(134);
   end
   else begin
      intensity := mstr(135);
   end
   Reply(intensity);
   NOption(138, Medic19, 4);
   NOption(139, Medic22, 4);
   NOption(140, Medic20, 4);
   NOption(141, Medic21, 6);
   NLowOption(142, Medic19);
   NLowOption(143, Medic22);
end

procedure Medic19// "okay fine, bye"
begin
   NMessage(144);
end

procedure Medic20 begin
   call Medic24;
   if not(Hours) then begin
      Reply(mstr(165) + mstr(166) + mstr(170));
   end
   else begin
      if Hours == 1 then begin
         if TempMinutes >= 30 then begin
            Reply(mstr(145) + Hours + mstr(169) + mstr(146));
         end
         else begin
            Reply(mstr(145) + mstr(166) + mstr(170));
         end
      end
      else begin
         if TempMinutes >= 30 then begin
            Reply(mstr(145) + Hours + mstr(169) + mstr(146));
         end
         else begin
            Reply(mstr(145) + Hours + mstr(146));
         end
      end
   end
   NOption(147, Medic19, 4);
   NOption(148, Medic22, 4);
end

procedure Medic21 begin
   call Medic24;

   if Hours > 1 then begin
      if (removal > rads) or (removal == rads) then begin
         IfRounded := mstr(168);
      end
      else begin
         IfRounded := mstr(150);
      end
   end
   else begin
      if (removal > rads) or (removal == rads) then begin
         IfRounded := mstr(167);
      end
      else begin
         IfRounded := mstr(150);
      end
   end

   if not(Hours) then begin
      Reply(mstr(165) + mstr(166) + IfRounded + rounded + mstr(151));
   end
   else begin
      if Hours == 1 then begin
         if TempMinutes >= 30 then begin
            Reply(mstr(149) + Hours + mstr(169) + IfRounded + rounded + mstr(151));
         end
         else begin
            Reply(mstr(149) + mstr(166) + IfRounded + rounded + mstr(151));
         end
      end
      else begin
         if TempMinutes >= 30 then begin
            Reply(mstr(149) + Hours + mstr(169) + IfRounded + rounded + mstr(151));
         end
         else begin
            Reply(mstr(149) + Hours + IfRounded + rounded + mstr(151));
         end
      end
   end

   NOption(152, Medic19, 4);
   NOption(153, Medic22, 4);
end

procedure Medic22// yes, remove them
begin
   Reply(random(154, 155));
   NOption(164, Medic23, 1);
end

procedure Medic23// time fade & increase for removal
begin
   gfade_out(1);
   radiation_inc(dude_obj, -removal);
   game_time_advance(10 * 60 * Minutes);
   gfade_in(1);
   NMessage(156);
end

procedure Medic24// COMPUTE RADS TO HEAL, MINUTES, AND HOURS
begin
   removal := random(140, 160) + (dude_luck * 2);
   if (removal > rads) or (removal == rads) then begin
      Minutes := (rads * 2) + 30;// time per rad to heal. was 20.(!)
      rounded := rads;
   end
   else begin
      Minutes := (removal * 2) + 30;// time per rad to heal. was 20.(!)
      rounded := removal;
      while(rounded % 5) do begin// round the number.
         rounded := rounded - 1;
      end
   end
   TempMinutes := Minutes;
   Hours := 0;
   while(TempMinutes >= 60) do begin
      TempMinutes := (TempMinutes - 60);
      Hours := (Hours + 1);
   end
end

procedure MedicRadiationFadeOutIn begin
   gfade_out(1);
   gfade_in(1);
   call Medic18;
end

procedure MedicRadiationHealRequest// irradiated heal request
begin
   Reply(137);
   NOption(164, MedicRadiationFadeOutIn, 1);
end

procedure Medic27// Lol dumb if already got drugs twice
begin
   BMessage(125); // Stimpaks are being rationed.
end

procedure Medic28//stupid character, give drugs
begin
   if local_var(LVAR_ReceivedSupplies) == 1 then begin
      set_local_var(LVAR_ReceivedSupplies, 2);
   end
   NMessage(172);
   call GiveDrugs;
end

procedure Medic29 begin
   variable Stimpak := 0;
   Stimpak := create_object(PID_STIMPAK, 0, 0);
   add_mult_objs_to_inven(dude_obj, Stimpak, 1);
   NMessage(111);
end

#define give_drug(_pid, _amount) drugPtr := create_object(PID_STIMPAK, 0, 0); \
                                 add_mult_objs_to_inven(dude_obj, drugPtr, _amount)

procedure GiveDrugs begin
   variable drugPtr := 0;
   variable amount  := 0;
   amount := 2 - difficulty_level;
   if amount then begin
      give_drug(PID_STIMPAK, amount);
      give_drug(PID_BUFFOUT, amount);
   end
   give_drug(PID_SUPER_STIMPAK, 1);
   give_drug(PID_PSYCHO, 1);
end

procedure MedicEnd begin
end

procedure Node2000 begin
   if (local_var(LVAR_Had_Toe_Removed) == 0) then begin
      NMessage(2001);
      Reply(2002);
   end
   else begin
      Reply(2003);
   end

   NOption(2010, Node3000, 004);
   NOption(2020, MedicMainMenu, 004);
   NOption(2030, medicend, 004);
end

procedure Node3000 begin
variable toe;

   NMessage(3000);

   gfade_out(600);

   // Remove mutation
   if (dude_rads > 50) then begin
      set_global_var(GVAR_HAVE_MUTATED, 0);
      set_global_var(GVAR_MUTATE_STAGE, 1);
      set_global_var(GVAR_MUTATE_WHEN, GAME_TIME_IN_DAYS);
   end
   else begin
      set_global_var(GVAR_HAVE_MUTATED, 0);
      set_global_var(GVAR_MUTATE_STAGE, 0);
   end

   // Give player a mutated "toe"
   toe := create_object(PID_MUTATED_TOE, 0, 0);
   add_obj_to_inven(dude_obj, toe);
   gfade_in(600);

   Reply(3001);

   if (local_var(LVAR_Had_Toe_Removed) == 0) then begin
      set_local_var(LVAR_Had_Toe_Removed, 1);
      NOption(3010, MedicMainMenu, 004);
   end
   else
      NOption(3020, MedicEnd, 004);
   NOption(3030, MedicEnd, 004);
end
