/*

   Supermutant Nightkin

*/

/* Include Files */
#include "define.h"
#include "maps/lafollwr.h"
#include "sfall/main.h"

#define NAME                    SCRIPT_FLSUPMUT
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure combat_p_proc;

#define LVAR_AssignName       (0)

variable tmp_hostile;
variable initial := 0;
variable in_timed_event;
variable weaponPtr;

procedure start begin
   if not(initial) then begin
      initial := 1;
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      if (local_var(LVAR_AssignName) == 0) then begin
         set_local_var(LVAR_AssignName, (100 + map_var(MVAR_COC_FLMUT_NAME)));
         inc_map_var(MVAR_COC_FLMUT_NAME);

         weaponPtr := create_object(PID_MINIGUN, 0, 0);
         add_obj_to_inven(self_obj, weaponPtr);
         wield_obj_critter(self_obj, weaponPtr);
         add_obj_to_inven(self_obj, create_object(PID_5MM_JHP, 0, 0));

         spawn_stealth_boy
      end
      self_rotate(random(0, 5));
   end
end

procedure critter_p_proc begin
   if (global_var(GVAR_ENEMY_FOLL_INVADERS)) then begin
      tmp_hostile := 1;
   end
   if (tmp_hostile and self_can_see_dude) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (not(in_timed_event)) then begin
         in_timed_event := 1;
         add_timer_event(self_obj, game_ticks(random(6, 10)), random(1, 3));
      end
   end
end

procedure destroy_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_FOLL_INVADERS, 1);
   end
   inc_evil_critter
end

procedure look_at_p_proc begin
   display_msg(mstr(local_var(LVAR_AssignName)));
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   variable LVar0 := 0;
   dude_look_at_self;

   LVar0 := random(110, 118);
   float_msg(self_obj, mstr(LVar0), FLOAT_MSG_NORMAL);
   if ((LVar0 == 112) or (LVar0 == 117)) then begin
      tmp_hostile := 1;
   end
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then
      self_rotate(random(0, 5));
   else
      self_walk_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(1, 3)));
   in_timed_event := 0;
end

procedure combat_p_proc begin
   activate_stealth_boy
end

