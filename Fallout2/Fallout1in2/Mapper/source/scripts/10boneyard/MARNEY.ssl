/*

   Followers of the Apocalypse - Marney the nurse / doctor. cut-content.

   NOTE:

      This "one" character is actually two different characters.
      At night time it is Dr. Solone, M.D. and at day Nurse Marney.
      Obviously this causes oddities ... so this better be splitted at some point.

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_MARNEY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_update_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;
procedure pickup_p_proc;

procedure Marney00;
procedure Marney01;
procedure Marney02;
procedure Marney03;
procedure Marney04;
procedure Marney05;
procedure Marney06;
procedure Marney07;
procedure MarneyEnd;

#define LVAR_LearnFirstAid          (1)
#define LVAR_LearnDoctor            (2)

variable initial := 0;

procedure start begin
   if not(initial) then begin
      set_self_team(TEAM_LA_FOLLOWERS);
      set_self_ai(AI_FOLLOWER_LEADER);
      initial := 1;
   end
   if not(fo1in2_cut_characters_enabled) then destroy_object(self_obj);
end

procedure map_update_p_proc begin
   if night then begin
      art_change_fid_num(self_obj, FID_NFMAXX);
   end
   else begin
      art_change_fid_num(self_obj, FID_HFMAXX);
   end
end

procedure critter_p_proc begin
   if (global_var(GVAR_QUEST_BONEYARD_3_BOMBFOLL) == 2) then begin
      kill_critter(self_obj, ANIM_exploded_to_nothing_sf);
   end

   if (self_can_see_dude) then begin
      if (global_var(GVAR_ENEMY_FOLLOWERS) == 1) then begin
         attack(dude_obj);
      end
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_ENEMY_FOLLOWERS, 1);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(0)) then begin
      if night then begin
         display_msg(mstr(100));
      end
      else begin
         display_msg(mstr(101));
      end
   end
   else begin
      if night then begin
         display_msg(mstr(102));
      end
      else begin
         display_msg(mstr(103));
      end
   end
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_FOLLOWER_LVL3);

   dude_look_at_self;
   set_local_var(0, 1);
   start_dialog_at_node(Marney00);
end

procedure pickup_p_proc begin
   set_global_var(GVAR_ENEMY_FOLLOWERS, 1);
end

procedure Marney00 begin
   variable hurt := 0;
   hurt := dude_max_hp - dude_cur_hp;

   Reply(104);
   NLowOption(105, Marney01);
   NOption(125, MarneyEnd, 4);
   if (hurt > 0) then begin
      NOption(106, Marney01, 4);
   end
   if ((local_var(LVAR_LearnDoctor) == 0) and night) then begin
      NOption(107, Marney06, 5);
   end
end

procedure Marney01 begin
   Reply(108);
   NLowOption(109, Marney02);
   NLowOption(110, Marney05);
   NOption(111, Marney02, 4);
   NOption(112, Marney05, 4);
end

procedure Marney02 begin
   variable LVar0 := 0;

   gfade_out(1);
      LVar0 := dude_max_hp - dude_cur_hp;
      game_time_advance(game_ticks(60 * 20 * LVar0));
      critter_heal(dude_obj, LVar0);
      critter_uninjure(dude_obj, (DAM_CRIP_LEG_LEFT bwor DAM_CRIP_LEG_RIGHT bwor DAM_CRIP_ARM_LEFT bwor DAM_CRIP_ARM_RIGHT));
   gfade_in(1);

   Reply(113);
   if ((local_var(LVAR_LearnFirstAid) == 0) and day) then begin
      NOption(114, Marney03, 4);
   end
   NOption(115, MarneyEnd, 4);
end

procedure Marney03 begin
   Reply(116);
   NOption(117, Marney04, 4);
   NOption(118, Marney05, 4);
end

procedure Marney04 begin
   gfade_out(1);
      game_time_advance(game_ticks(18000));
      critter_mod_skill(dude_obj, SKILL_FIRST_AID, 15);
      display_msg(gen_mstr(116) + 15 + gen_mstr(123));
      set_local_var(LVAR_LearnFirstAid, 1);
   gfade_in(1);
   NMessage(119);
end

procedure Marney05 begin
   NMessage(120);
end

procedure Marney06 begin
   Reply(121);
   NOption(122, Marney07, 5);
   NOption(123, Marney05, 5);
end

procedure Marney07 begin
   gfade_out(1);
      game_time_advance(game_ticks(25200));
      critter_mod_skill(dude_obj, SKILL_DOCTOR, 15);
      display_msg(gen_mstr(116) + 15 + gen_mstr(124));
      set_local_var(LVAR_LearnDoctor, 1);
   gfade_in(1);
   NMessage(124);
end

procedure MarneyEnd begin
end
