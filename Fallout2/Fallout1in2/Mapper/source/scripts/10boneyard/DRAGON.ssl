#include "define.h"

#define NAME                    SCRIPT_DRAGON

#include "command.h"

procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure show_true_name;
procedure show_false_name;
procedure Dragon00;
procedure Dragon01;
procedure Dragon02;
procedure Dragon03;
procedure Dragon04;
procedure Dragon05;
procedure Dragon06;
procedure Dragon07;
procedure Dragon08;
procedure Dragon09;
procedure Dragon10;
procedure Dragon11;
procedure DragonCombat;
procedure DragonEnd;

variable tmp_hostile;
variable initial := 0;

procedure start begin
   if not(initial) then begin
      set_self_team(TEAM_LA_BLADES);
      set_self_ai(AI_BLADES);
      initial := 1;
   end
end

procedure critter_p_proc begin
   if (day and (self_tile != 26746)) then begin
      self_walk_to_tile(26746);
   end
   else begin
      if ((game_time_hour >= 1900) or (game_time_hour < 600) and (self_tile != 23938)) then begin
         self_walk_to_tile(23938);
      end
   end
   if (self_can_see_dude and (global_var(GVAR_BLADES_LEFT) < 41)) then begin
      tmp_hostile := 1;
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure destroy_p_proc begin

   set_global_var(GVAR_BLADES_LEFT, global_var(GVAR_BLADES_LEFT) - 1);
   inc_good_critter
end

procedure look_at_p_proc begin
   if ((global_var(GVAR_QUEST_BONEYARD_1_ME_BLADE) == 2) or (dude_luck > 6)) then begin
      call show_true_name;
   end
   else if (dude_luck < 4) then begin
      call show_false_name;
   end
   else if (dude_iq < 5) then begin
      call show_false_name;
   end
   else if (random(0, 1)) then begin
      call show_false_name;
   end
   else begin
      call show_true_name;
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (dude_iq < 4) then begin
      display_msg(gen_mstr(175));
   end
   else begin
      start_gdialog(284, self_obj, 4, -1, -1);
      gsay_start;
      set_local_var(0, 1);
      if day then begin
         call Dragon00;
      end
      else begin
         if (global_var(GVAR_QUEST_BONEYARD_1_ME_BLADE) == 1) then begin
            call Dragon07;
         end
         else if (global_var(GVAR_QUEST_BONEYARD_1_ME_BLADE) == 2) then begin
            call Dragon11;
         end
         else begin
            call Dragon01;
         end
      end
      gsay_end;
      end_dialogue;
   end
end

procedure show_true_name begin
   script_overrides;
   if (local_var(0)) then begin
      display_msg(mstr(100));
   end
   else begin
      display_msg(mstr(103));
   end
end

procedure show_false_name begin
   script_overrides;
   display_msg(mstr(random(104, 110)));
end

procedure Dragon00 begin
   NMessage(111);
end

procedure Dragon01 begin
   Reply(112);
   NOption(113, Dragon02, 4);
   NOption(114, Dragon06, 4);
   NOption(115, DragonCombat, 4);
end

procedure Dragon02 begin
   Reply(116);
   NOption(117, Dragon03, 4);
   NOption(118, Dragon04, 4);
   NOption(119, Dragon05, 4);
end

procedure Dragon03 begin
   NMessage(120);
   call DragonCombat;
end

procedure Dragon04 begin
   NMessage(121);
end

procedure Dragon05 begin
   NMessage(122);
   call DragonCombat;
end

procedure Dragon06 begin
   Reply(123);
   NOption(124, DragonEnd, 1);
   NOption(125, DragonCombat, 1);
end

procedure Dragon07 begin
   Reply(126);
   NOption(127, DragonCombat, 4);
   NOption(128, Dragon08, 4);
   NOption(129, Dragon10, 4);
end

procedure Dragon08 begin
   if (dude_item_count(PID_TANGLERS_HAND)) then begin
      NMessage(130);
   end
   else begin
      call Dragon09;
   end
end

procedure Dragon09 begin
   NMessage(131);
end

procedure Dragon10 begin
   NMessage(132);
end

procedure Dragon11 begin
   NMessage(133);
end

procedure DragonCombat begin
   tmp_hostile := 1;
end

procedure DragonEnd begin
end
