/*

   Boneyard - Dolgan / Snake, Blades member

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_DOLGAN
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure show_true_name;
procedure show_false_name;
procedure Dolgan00;
procedure Dolgan01;
procedure Dolgan02;
procedure Dolgan03;
procedure Dolgan04;
procedure Dolgan05;
procedure Dolgan06;
procedure Dolgan07;
procedure Train;
procedure DolganEnd;

variable tmp_hostile;
variable initial := 0;

procedure DolganCombat;

procedure start begin
   if not(initial) then begin
      set_self_team(TEAM_LA_BLADES);
      set_self_ai(AI_BLADES);
      initial := 1;
   end
end

procedure critter_p_proc begin
   if (self_can_see_dude and (global_var(GVAR_BLADES_LEFT) < 41)) then begin
      tmp_hostile := 1;
   end
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure destroy_p_proc begin
   set_global_var(GVAR_BLADES_LEFT, global_var(GVAR_BLADES_LEFT) - 1);
   inc_good_critter
end

procedure look_at_p_proc
begin
   if ((global_var(GVAR_QUEST_BONEYARD_1_ME_BLADE) == 2) or (dude_luck > 6)) then begin
      call show_true_name;
   end
   else begin
      if (dude_luck < 4) then begin
         call show_false_name;
      end
      else begin
         if (dude_iq < 5) then begin
            call show_false_name;
         end
         else begin
            if (random(0, 1)) then begin
               call show_false_name;
            end
            else begin
               call show_true_name;
            end
         end
      end
   end
end

procedure pickup_p_proc
begin
   tmp_hostile := 1;
end

procedure talk_p_proc
begin
   dude_look_at_self;
   start_gdialog(282, self_obj, 4, -1, -1);
   gsay_start;
   set_local_var(0, 1);
   if (global_var(GVAR_QUEST_BONEYARD_1_ME_BLADE) == 2) then begin
      call Dolgan03;
   end
   else begin
      if ((global_var(GVAR_QUEST_BONEYARD_1_ME_BLADE) == 1) and (local_var(1) == 0)) then begin
         call Dolgan01;
      end
      else begin
         call Dolgan00;
      end
   end
   gsay_end;
   end_dialogue;
end

procedure show_true_name
begin
   script_overrides;
   if (local_var(0)) then begin
      if night then begin
         display_msg(mstr(100));
      end
      else begin
         display_msg(mstr(101));
      end
   end
   else begin
      display_msg(mstr(104));
   end
end

procedure show_false_name
begin
   script_overrides;
   display_msg(mstr(random(105, 111)));
end

procedure Dolgan00
begin
   float_msg(self_obj, mstr(112), FLOAT_MSG_RED);
end

procedure Dolgan01
begin
   float_msg(self_obj, mstr(113), FLOAT_MSG_RED);
   set_local_var(1, 1);
   critter_mod_skill(dude_obj, SKILL_MELEE, 7);
   game_time_advance(game_ticks(14400));
   call Dolgan02;
   display_msg(gen_mstr(116) + 7 + gen_mstr(121));
end

procedure Dolgan02
begin
   NMessage(114);
end

procedure Dolgan03
begin
   Reply(115);
   if (local_var(1) != 2) then begin
      NOption(116, Dolgan04, 5);
   end
   NOption(117, Dolgan07, 5);
   NOption(118, DolganEnd, 5);
end

procedure Dolgan04
begin
   Reply(119);
   if (local_var(1) != 2) then begin
      NOption(120, Train, 4);
   end
   NOption(121, DolganEnd, 4);
end

procedure Dolgan05
begin
   NMessage(122);
end

procedure Dolgan06
begin
   NMessage(123);
end

procedure Dolgan07
begin
   Reply(124);
   NOption(125, DolganEnd, 5);
end

procedure Train
begin
   game_time_advance(game_ticks(21600));
   set_local_var(1, 2);
   critter_mod_skill(dude_obj, SKILL_MELEE, 7);
   call Dolgan05;
   display_msg(gen_mstr(116) + 7 + gen_mstr(121));
end

procedure DolganEnd
begin
end

procedure DolganCombat
begin
   tmp_hostile := 1;
end
