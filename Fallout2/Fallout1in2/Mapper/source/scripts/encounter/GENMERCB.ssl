/*

   Encounter - Generic merchant

*/

#include "define.h"

#define NAME                    SCRIPT_GENMERCB
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "command.h"
#include "modreact.h"

procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

variable dude_attacked_guards;
variable dude_attacked_raiders;
variable initial := 0;
variable raiders_left;
variable rewarded;
variable scared;

import variable random_seed_1;
import variable random_seed_2;
import variable random_seed_3;

procedure start begin
   if not(initial) then begin
      set_self_team(TEAM_HUB_WATER_MERCHANTS);
      set_self_ai(AI_MERCHANT);
      scared := global_var(GVAR_ENEMY_RANDOM_ENC);
      initial := 1;
   end
end

procedure critter_p_proc begin
   if (scared and (tile_distance_objs(dude_obj, self_obj) < 8)) then begin
      call flee_dude;
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      scared := 1;
      set_global_var(GVAR_ENEMY_RANDOM_ENC, 1);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure pickup_p_proc begin
   scared := 1;
   set_global_var(GVAR_ENEMY_RANDOM_ENC, 1);
end

procedure talk_p_proc begin
   dude_look_at_self;
   raiders_left := random_seed_1;

   dude_attacked_raiders := random_seed_2;
   dude_attacked_guards := random_seed_3;

   if (scared) then begin
      float_msg(self_obj, mstr(104), FLOAT_MSG_RED);
   end
   else begin
      if ((raiders_left == 0) and dude_attacked_raiders) then begin
         if (rewarded) then begin
            float_msg(self_obj, mstr(101), FLOAT_MSG_BLUE);
         end
         else begin
            if (dude_is_male) then
               float_msg(self_obj, mstr(106), FLOAT_MSG_BLUE);
            else
               float_msg(self_obj, mstr(107), FLOAT_MSG_BLUE);

            rewarded := 1;
            move_obj_inven_to_obj(self_obj, dude_obj);
            inc_general_rep(3);

            display_msg(mstr(110));
            give_xp(50);
         end
      end
      else if (dude_attacked_guards) then begin
         float_msg(self_obj, mstr(103), FLOAT_MSG_RED);
      end
      else if (raiders_left != 0) then begin
         if (random(0, 1)) then
            float_msg(self_obj, mstr(105), FLOAT_MSG_GREEN);
         else
            float_msg(self_obj, mstr(102), FLOAT_MSG_GREEN);
      end
   end
end
