/*

   Junktown - Center / Killian's store, Crash House, etc.

*/

#include "define.h"
#include "area/junktown.h"
#include "maps/junkkill.h"

#include "command.h"
#include "updatmap.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;

procedure Invasion;

#define store_out_of_business1_hex    (29295)
#define store_out_of_business2_hex    (25708)

variable party_elevation;
variable dude_start_hex;

export variable raider_dead;
export variable Sinthia_dead;
export variable award;
export variable assassin_seed;
export variable KillSafe_ptr;
export variable Killian_store1_ptr;
export variable Killian_store2_ptr;
export variable Killian_store3_ptr;
export variable Killian_barter_var;
export variable jail_door_ptr;
export variable messing_with_fridge;
export variable weapon_checked;
export variable sneak_checked;
export variable times_caught_sneaking;
export variable removal_ptr;

export variable JTRaider_ptr;
export variable Killian_ptr;
export variable Neal_ptr;
export variable Sinthia_ptr;
export variable Trish_ptr;
export variable Kenji_ptr;
export variable Marcelle_ptr;

variable item;

variable invaderPtr;

procedure start begin
end

procedure map_enter_p_proc begin
   set_global_var(GVAR_MARK_JUNK_2, 1);
   Lighting;

   call Invasion;

   override_map_start(99, 113, 0, 0);

   if not(junktown_invaded) then begin
      if (map_var(MVAR_hodiction) == 1) then begin
         display_msg(message_str(SCRIPT_SINTHIA, random(139, 140)));
      end

      if (not(is_killian_alive) /*and (global_var(GVAR_HIRED_BY_GIZMO) >= 2)*/) then begin
         if (tile_contains_pid_obj(store_out_of_business1_hex, 0, PID_OUT_OF_BUSINESS_NORTH) == 0) then begin
            variable KillianLights_List, hex;
            KillianLights_List := [25301,25693, 25685, 28098, 26079, 25871, 27472, 27883];
            foreach hex in KillianLights_List begin
               Scenery_Creation_Ptr := tile_contains_pid_obj(hex, 0, PID_LIGHT_SOURCE);
               destroy_object(Scenery_Creation_Ptr);
            end
         end
         create_north_out_of_business(store_out_of_business1_hex, 0);
         create_east_out_of_business(store_out_of_business2_hex, 0);
      end
   end
end

procedure map_update_p_proc begin
   Lighting;
   if (award and not(map_var(MVAR_Sinthia_award)) and (global_var(GVAR_SAVE_SINTHIA) == 2)) then begin
      give_xp_msg(award, message_str(SCRIPT_SINTHIA, 176));
      set_map_var(MVAR_Sinthia_award, 1);
   end

   if (not(is_killian_alive) and (global_var(GVAR_KILLIAN_SOMETHING_1) == 1)) then begin
      set_global_var(GVAR_KILLIAN_SOMETHING_1, 2);

      give_xp_msg(EXP_KILLED_KILLIAN, message_str(SCRIPT_KILLIAN, 269));
      dec_general_rep(5);
      inc_junktown_rep(REP_BONUS_KILLED_KILLIAN);
      set_global_var(GVAR_ENEMY_JUNKTOWN, 1);
   end

   if (removal_ptr) then begin
      destroy_object(removal_ptr);
      removal_ptr := 0;
   end
end

procedure Invasion begin
   if (junktown_invaded and not(is_loading_game) and (map_var(MVAR_Invasion) == 0)) then begin
      set_map_var(MVAR_Invasion, 1);

      kill_critter_type(PID_BRAHMIN, 1);
      kill_critter_type(PID_LOSER, 1);
      kill_critter_type(PID_TOWNSMAN_LEATHER, 1);
      kill_critter_type(PID_KILLIAN, 0);
      kill_critter_type(PID_PEASANT_GREEN, 1);
      kill_critter_type(PID_PEASANT_5, 1);
      kill_critter_type(PID_TOWNSWOMAN_2, 1);
      kill_critter_type(PID_PEASANT_FEMALE_CUTE, 1);
      kill_critter_type(PID_PEASANT_2_FEMALE, 0);
      kill_critter_type(PID_VINNIE, 0);
      kill_critter_type(PID_THUG, 0);
      kill_critter_type(PID_STRANGER_2, 0);

      invaderPtr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(invaderPtr, AI_SUPER_MUTANT_AGGRESSIVE);
      critter_attempt_placement(invaderPtr, 28885, 0);

      invaderPtr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(invaderPtr, AI_SUPER_MUTANT_AGGRESSIVE);
      critter_attempt_placement(invaderPtr, 21671, 0);

      invaderPtr := create_object_sid(PID_MAD_SUPER_MUTANT, 0, 0, SCRIPT_INVADER);
      set_ai(invaderPtr, AI_SUPER_MUTANT_AGGRESSIVE);
      critter_attempt_placement(invaderPtr, 21328, 0);

      // Any player party member was waiting in the map before?
      check_invasion_party_waiting;
   end
end
