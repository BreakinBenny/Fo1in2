/*

   Junktown - Cougar, Doc Morbid guard

*/

/* Include Files */
#include "define.h"
//#include "area/necropolis.h"

#define NAME                    SCRIPT_COUGAR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;

procedure Cougar00;
procedure Cougar01;
procedure Cougar02;
procedure Cougar03;
procedure Cougar04;

variable tmp_hostile;

import variable fetch_dude;

procedure damage_p_proc;

procedure start begin
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (fetch_dude) then begin
         tmp_hostile := 1;
         fetch_dude := 0;
      end
   end
   if (global_var(GVAR_IS_MORBID_TEAM_HOSTILE) == 1) then begin
      if (self_can_see_dude) then begin
         attack(dude_obj);
      end
   end
end

procedure map_enter_p_proc begin
   if junktown_invaded then begin
      kill_critter(self_obj, ANIM_fall_back_sf);
   end
   set_self_team(TEAM_JUNKTOWN_MORBID);
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   set_tma_data_generic(TMA_MSG_JUNKENT);

   dude_look_at_self;
   if (local_var(0)) then begin
      call Cougar04;
   end
   else begin
      start_dialog_at_node(Cougar00);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure Cougar00 begin
   set_local_var(0, 1);
   Reply(101);
   NOption(102, Cougar01, 4);
   NOption(103, Cougar02, 5);
   NLowOption(104, Cougar03);
end

procedure Cougar01 begin
   Reply(105);
   NOption(106, Cougar02, 4);
end

procedure Cougar02 begin
   NMessage(107);
end

procedure Cougar03 begin
   NMessage(108);
end

procedure Cougar04 begin
   float_msg(self_obj, mstr(109), FLOAT_MSG_NORMAL);
end

procedure damage_p_proc begin
   if source_is_dude then begin
      set_global_var(GVAR_IS_MORBID_TEAM_HOSTILE, 1);
   end
end
