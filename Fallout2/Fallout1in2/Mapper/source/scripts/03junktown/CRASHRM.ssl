/*

   Junktown - Hotel room spatial script

*/

/* Include Files */
#include "define.h"
#include "maps/junkkill.h"

#define NAME                    SCRIPT_CRASHRM

#include "command.h"

/* Standard Script Procedures */
procedure start;
procedure spatial_p_proc;
procedure talk_p_proc;

procedure CrashRm00;
procedure CrashRm01;
procedure CrashRm02;
procedure CrashRmEnd;

variable item;

import variable JTRaider_ptr;
import variable Sinthia_ptr;

#define LVAR_Only_Once     (0)

procedure start begin
end

procedure spatial_p_proc begin
   if (source_is_dude and (global_var(GVAR_RENT_TIME) > (GAME_TIME_IN_DAYS))) then begin
      dialogue_system_enter;
   end
end

procedure talk_p_proc begin
   start_dialog_at_node(CrashRm00);

   if (local_var(LVAR_Only_Once) == 1) then begin
      call CrashRm02;
   end
end

procedure CrashRm00 begin
   Reply(100);
   NOption(101, CrashRm01, 1);
   NOption(102, CrashRmEnd, 1);
end

procedure CrashRm01 begin
   set_local_var(LVAR_Only_Once, 1);
end

procedure CrashRm02 begin
   set_local_var(LVAR_Only_Once, 0);
   gfade_out(1);
      game_time_advance(game_ticks(60 * (60 - (game_time_hour % 100))));
      if (game_time_hour < 1000) then begin
         game_time_advance(game_ticks(36 * (1000 - game_time_hour)));
      end
      else begin
         game_time_advance(game_ticks(36 * (3400 - game_time_hour)));
      end
      critter_heal(dude_obj, random(10, 15));
      display_msg(mstr(103));

      if ((map_var(MVAR_save_da_ho) == 0) and (map_var(MVAR_Sinthia_dead) != 2)) then begin
         JTRaider_ptr := create_object_sid(PID_RAIDER_GUARD, 17484, 0, SCRIPT_JTRAIDER);
         move_to(Sinthia_ptr, 17485, 0);
         obj_rotate(JTRaider_ptr, 3);
         obj_rotate(Sinthia_ptr, 3);

         item := create_object(PID_10MM_PISTOL, 0, 0);
         add_obj_to_inven(JTRaider_ptr, item);
         wield_obj_critter(JTRaider_ptr, item);
         item := create_object(PID_LEATHER_ARMOR, 0, 0);
         add_obj_to_inven(JTRaider_ptr, item);
         wield_obj_critter(JTRaider_ptr, item);

         set_map_var(MVAR_save_da_ho, 1);
      end
   gfade_in(1);
end

procedure CrashRmEnd begin
end
