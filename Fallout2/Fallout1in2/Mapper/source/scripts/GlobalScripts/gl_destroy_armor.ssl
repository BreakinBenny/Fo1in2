/*

   Should armor get destroyed on critter death?

*/

/* Include Files */
#include "define.h"
#include "command.h"

#include "sfall/main.h"

/* Standard Script Procedures */
procedure start;
procedure destroy_armor;

// Script would delete default armor from Ian, Tycho, and Katja on map_enter
#define not_possible_partymember    (obj_pid(critter) != PID_IAN and obj_pid(critter) != PID_TYCHO and obj_pid(critter) != PID_KATJA and obj_pid(critter) != PID_VASQUEZ)

procedure start begin
   if (game_loaded) then begin
      set_global_script_type(0);
      if fo1in2_destroy_armor_enabled then begin
         register_hook_proc(HOOK_STDPROCEDURE, destroy_armor);
      end
   end
end

procedure destroy_armor begin
   variable
      proc := get_sfall_arg,
      critter := get_sfall_arg;

   if (proc != destroy_proc) then return;
   if (obj_type(critter) == OBJ_TYPE_CRITTER) then begin
      if critter_wearing_armor(critter) and (critter != dude_obj) and not(obj_in_party(critter)) and not_possible_partymember then begin
         variable Item := get_armor(critter);
         set_flags(item, get_flags(item) bwand bwnot(FLAG_WORN));
         rm_obj_from_inven(critter, item);
         debug_msg("destroying critter armor!");
      end
   end
end
