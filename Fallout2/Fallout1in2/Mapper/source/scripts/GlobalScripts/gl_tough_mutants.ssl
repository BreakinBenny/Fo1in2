/*

   Makes the super mutants a lot tougher with overriding their base hp value.

*/

/* Include Files */
#include "define.h"
#include "command.h"

#include "sfall/main.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure adjust_hp;

#define crit_pid           obj_pid(critter)
#define crit_max_hp        get_max_hp(critter)

#define hp_overkill        global_var(GVAR_ENABLE_TOUGH_MUTANTS)

#define hp_base            get_proto_data(crit_pid, 64)
#define inc_hp(x)          set_proto_data(crit_pid, PROTO_CR_BONUS_HP, (x * hp_overkill)); \
                           if map_first_run then critter_heal(critter, crit_max_hp)
#define new_hp(x)          inc_hp((x - hp_base))

#define inc_plasma_res(x)  set_proto_data(crit_pid, PROTO_AR_DR_PLASMA, x)

procedure start begin
   call adjust_hp;
end

procedure map_enter_p_proc begin
   call adjust_hp;
end

procedure adjust_hp begin
   variable critter;
   if fo1in2_tough_mutants_enabled then begin
      if (cur_map_index > MAP_DESERT0) then begin
         foreach (critter in list_as_array(LIST_CRITTERS)) begin
            /*
               Calculation: inc_hp = wanted HP - (orig hp - bonus)
            */

            // No change to the Master, he is strong enough.
            if (crit_pid == PID_MASTER) then begin
               new_hp(500); // orig. hp is 500
            end

            // No change to the Lou, he is strong enough with his guards.
            else if (crit_pid == PID_LOU) then begin
               new_hp(250); // orig. hp is 250
            end

            // No change to Harry for early game balance
            else if (crit_pid == PID_HARRY) then begin
               new_hp(120); // orig. hp is 120
            end

            else if (crit_pid == PID_NIGHTKIN) then begin
               //debug("plasma: " + get_proto_data(crit_pid, PROTO_AR_DR_PLASMA));
               new_hp(100); // orig. hp is 50
            end
            else if (crit_pid == PID_NIGHTKIN_GUARD) then begin
               new_hp(125); // orig. hp is 60
            end

            // Don't change the Watershed mutants, for early game balance
            else if (crit_pid == PID_SUPER_MUTANT and not(CUR_MAP_WATRSHD)) then begin
               new_hp(80); // orig. hp is 40
            end
            else if (crit_pid == PID_TOUGH_SUPER_MUTANT) then begin
               new_hp(115); // orig. hp is 75
            end

            else if (crit_pid == PID_SUPER_MUTANT_GUARD) then begin
               new_hp(90); // orig. hp is 50
            end
            else if (crit_pid == PID_MEAN_SUPER_MUTANT) then begin
               new_hp(100); // orig. hp is 60
            end
            // Don't change the Necropolis invasion mutants, for early game balance
            else if ((crit_pid == PID_MAD_SUPER_MUTANT) and not(CUR_AREA_NECROPOLIS)) then begin
               new_hp(150); // orig. hp is 100
            end
            else if (crit_pid == PID_DEADLY_SUPER_MUTANT) then begin
               new_hp(175); // orig. hp is 125
            end

            else if (crit_pid == PID_TOUGH_NIGHTKIN) then begin
               new_hp(140); // orig. hp is 70
            end
            else if (crit_pid == PID_DEADLY_NIGHTKIN) then begin
               new_hp(160); // orig. hp is 90
            end
            else if (crit_pid == PID_SUPER_NIGHTKIN) then begin
               new_hp(180); // orig. hp is 1101
            end
            else if (crit_pid == PID_MASTER_NIGHTKIN) then begin
               new_hp(200); // orig. hp is 140
            end
         end
      end
   end
end
