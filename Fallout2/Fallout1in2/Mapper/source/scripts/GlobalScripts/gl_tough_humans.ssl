/*

   Rebalances various humans in locations and encounters to make them tougher by overriding their base hp value.

*/

/* Include Files */
#include "define.h"
#include "command.h"

#include "sfall/main.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure adjust_hp;

#define crit_pid           obj_pid(critter)
#define crit_max_hp        get_max_hp(critter)

#define hp_overkill        global_var(GVAR_ENABLE_TOUGH_HUMANS)

#define hp_base            get_proto_data(crit_pid, 64)
#define inc_hp(x)          base := x;                                                           \
                           if (base <= 0 and hp_overkill > 1) then begin                        \
                              base := 10;                                                       \
                           end                                                                  \
                           set_proto_data(crit_pid, PROTO_CR_BONUS_HP, (base * hp_overkill));   \
                           if map_first_run then critter_heal(critter, crit_max_hp)
#define new_hp(x)          inc_hp((x - hp_base))

procedure start begin
   call adjust_hp;
end

procedure map_enter_p_proc begin
   call adjust_hp;
end

procedure adjust_hp begin
   variable critter;
   variable base;
   if fo1in2_tough_humans_enabled then begin
      //-- Khans
      if (CUR_MAP_RAIDERS) then begin
         foreach (critter in list_as_array(LIST_CRITTERS)) begin
            // No change to Garl, he is strong enough.
            if (crit_pid == PID_GARL) then begin
               new_hp(80); // orig. hp is 80
            end

            if (crit_pid == PID_GARLS_ADVISOR) then begin
               new_hp(55); // orig. hp is 33
            end
            if (crit_pid == PID_RAIDER) then begin
               new_hp(44); // orig. hp is 33
            end
            if (crit_pid == PID_RAIDER_GUARD) then begin
               new_hp(44); // orig. hp is 33
            end

            // "Citizen" Raider
            if (crit_pid == PID_CITIZEN_FEMALE_YELLOWPANTS) then begin
               new_hp(30); // orig. hp is 15
            end

            // "Punk" Raider
            if (crit_pid == PID_CITIZEN_PUNK) then begin
               new_hp(30); // orig. hp is 15
            end

            if (crit_pid == PID_CITIZEN_LEATHER_FEMALE) then begin
               new_hp(30); // orig. hp is 15
            end

         end
      end

      else if map_is_encounter then begin
         //-- Encounter around The Hub
         if (global_var(GVAR_WORLDMAP_TABLE) == 8) then begin
            foreach (critter in list_as_array(LIST_CRITTERS)) begin
               if (crit_pid == PID_MAN_JEANS) then begin
                  new_hp(60); // orig. hp is 30
               end
               if (crit_pid == PID_LEATHER_JACKET_WOMAN) then begin
                  new_hp(60); // orig. hp is 30
               end
               if (crit_pid == PID_MALE_GUARD_METAL_BLACK) then begin
                  new_hp(60); // orig. hp is 30
               end
               if (crit_pid == PID_WOMAN_YELLOW) then begin
                  new_hp(60); // orig. hp is 30
               end
            end
         end

         //-- Encounter around Necropolis
         else if (global_var(GVAR_WORLDMAP_TABLE) == 9) then begin
            foreach (critter in list_as_array(LIST_CRITTERS)) begin
               if (crit_pid == PID_ZOMBIE_GUARD) then begin
                  new_hp(55); // orig. hp is 36
               end
            end
         end

         //-- Encounter around Brotherhood
         else if (global_var(GVAR_WORLDMAP_TABLE) == 10) then begin
            foreach (critter in list_as_array(LIST_CRITTERS)) begin
               if (crit_pid == PID_PEASANT_ROBE) then begin
                  new_hp(40); // orig. hp is 30
               end
               if (crit_pid == PID_MALE_GUARD_METAL_BLACK) then begin
                  new_hp(60); // orig. hp is 29
               end
               if (crit_pid == PID_TOWNSWOMAN_METAL) then begin
                  new_hp(58); // orig. hp is 29
               end

               if (crit_pid == PID_GEN_PALAIN_1) then begin
                  new_hp(100); // orig. hp is 50
               end
               if (crit_pid == PID_GEN_PALAIN_2) then begin
                  new_hp(100); // orig. hp is 50
               end
               if (crit_pid == PID_JUNIOR_PALADIN) then begin
                  new_hp(78); // orig. hp is 39
               end
               if (crit_pid == PID_GEN_JUNIOR_PALADIN) then begin
                  new_hp(82); // orig. hp is 41
               end

               if (crit_pid == PID_MAN_JEANS) then begin
                  new_hp(60); // orig. hp is 30
               end
            end
         end

         //-- Encounter around Boneyard
         else if (global_var(GVAR_WORLDMAP_TABLE) == 13) then begin
            foreach (critter in list_as_array(LIST_CRITTERS)) begin
               if (crit_pid == PID_GEN_THUG) then begin
                  new_hp(72); // orig. hp is 36
               end
               if (crit_pid == PID_THUG_MALE_METAL) then begin
                  new_hp(78); // orig. hp is 39
               end
               if (crit_pid == PID_MAN_JEANS) then begin
                  new_hp(60); // orig. hp is 30
               end
            end
         end
      end // is encounter end
   end
end
